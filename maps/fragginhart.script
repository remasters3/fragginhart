game_manager
{
	spawn
	{
		// Game rules
		wm_axis_respawntime 30
		wm_allied_respawntime 20
		wm_number_of_objectives 8
		wm_set_round_timelimit 20
		wm_set_defending_team 0
		wm_setwinner 0
		
		// map config // 
		accum 0 set 0 // 0=config_default, 1=config_te, 2=config_rema, 3=config_uips
		
		// Objectives
		// 1: Generator
		accum 1 set	0
		wm_objective_status 1 1 0
		wm_objective_status 1 0 0

		// 2: Side Wall
		accum 2 set	0
		wm_objective_status 2 1 0
		wm_objective_status 2 0 0

		// 3: Forward Bunker
		accum 3 set	0
		wm_objective_status 3 1 0
		wm_objective_status 3 0 0

		// 4: Depot Gate Controls
		accum 4 set	0
		wm_objective_status 4 1 0
		wm_objective_status 4 0 0

		// 5: Documents
		accum 5 set	0
		wm_objective_status 5 1 0
		wm_objective_status 5 0 0

		// 6: Transmitter
		accum 6 set	0
		wm_objective_status 6 1 0
		wm_objective_status 6 0 0

		// 7: Axis Command Post
		accum 7 set	0
		wm_objective_status 7 1 0
		wm_objective_status 7 0 0

		// 8: Allied Command Post
		accum 8 set	0
		wm_objective_status 8 1 0
		wm_objective_status 8 0 0
		
		// assualt ramp
		accum 9 set	0

		wait 50

		// setautospawn "Middle Spawn" 0
		// setautospawn "Forward bunker" 0
		setautospawn "Generator Spawn" 0
		setautospawn "Villa" 1
		
		wait 1000
		trigger self phase1
	}
	
	trigger maptime_ph1
	{
		wm_set_round_timelimit 10
	}
	
	trigger maptime_ph2
	{
		wm_set_round_timelimit 20
	}
	
	trigger maptime_ph3
	{
		wm_set_round_timelimit 30
	}	
	
	trigger config_default
	{
		// wm_announce "^1< ^bconfig_default loaded ^1>"
		
		trigger villa_garage unblock             //blocks the route between the villa garage and the balcony north corridor
		trigger cheese_door enabled              //door that opens between north corridor and transmitter road when generator is captured.
		trigger battery_room unblock             //blocks the route between the transmitter and generator spawn
		trigger gen_gate enabled                 //door that opens for access to the rear of the generator and maingate
		trigger block_gen_sidewall_window false  //do u want to block the murderhole at generator?
		trigger uppermaingateoffice_door enabled //door that opens to the wooden rooms above maingate.
		trigger block_flag_sidewall false        //do u want to block the extra route between sidewall and flag?
		trigger depot_recep unblock              //blocks the route between the depo reception and depogate
		trigger depot_basement_block false       //do u want to block access to the depot basement?
		trigger depot_se_storeroom unblock       //blocks access to the depot southeast storeroom
		trigger docswindow unblock               //blocks the windows at documents.
		trigger Depot_lift enabled               //elevator at documents room.
		trigger mortarspot_tst false             //Hidden teleport to mortar spot.
		trigger trans_covdoor false              //Axis team door on roof of transmitter
		globalaccum 0 set 1                      //Map time bonus: 0="false" 1="true"
		globalaccum 1 set 2                      //generator charge time: 0="10s time reset". 1="10s", 2="20s", 3="30s". 1-3="time added". 
	}
	
	trigger config_te
	{
		// wm_announce "^1< ^bconfig_te loaded ^1>"
		
		trigger villa_garage block
		trigger cheese_door disable_open
		trigger battery_room block
		trigger gen_gate disabled
		trigger block_gen_sidewall_window true
		trigger uppermaingateoffice_door disabled
		trigger block_flag_sidewall true
		trigger depot_recep block
		trigger depot_basement_block true
		trigger depot_se_storeroom block
        trigger docswindow block
		trigger Depot_lift disabled_up
		trigger mortarspot_tst false
		trigger trans_covdoor true
		globalaccum 0 set 0
		globalaccum 1 set 1
	}
	
	trigger config_rema
	{
		// wm_announce "^1< ^bconfig_rema loaded ^1>"
		
		trigger villa_garage unblock
		trigger cheese_door enabled
		trigger battery_room unblock
		trigger gen_gate enabled
		trigger block_gen_sidewall_window false
		trigger uppermaingateoffice_door enabled
		trigger block_flag_sidewall false
		trigger depot_recep block
		trigger depot_basement_block true
		trigger depot_se_storeroom block
		trigger docswindow unblock
		trigger Depot_lift enabled
		trigger mortarspot_tst true
		trigger trans_covdoor false
		globalaccum 0 set 1
		globalaccum 1 set 2
	}
	trigger config_uips
	{
		// wm_announce "^1< ^bconfig_uips loaded ^1>"	
		
		trigger villa_garage unblock            
		trigger cheese_door enabled             
		trigger battery_room unblock            
		trigger gen_gate enabled                
		trigger block_gen_sidewall_window false 
		trigger uppermaingateoffice_door enabled
		trigger block_flag_sidewall false       
		trigger depot_recep unblock             
		trigger depot_basement_block false      
		trigger depot_se_storeroom block        
		trigger docswindow unblock              
		trigger Depot_lift enabled              
		trigger mortarspot_tst false            
		trigger trans_covdoor true
		globalaccum 0 set 0
		globalaccum 1 set 3
	}
	
	trigger phase1
	{
		// waiting for entities to load.
		wait 500
		
		// load map config //
		accum 0 trigger_if_equal 0 game_manager config_default
		accum 0 trigger_if_equal 1 game_manager config_te
		accum 0 trigger_if_equal 2 game_manager config_rema
		accum 0 trigger_if_equal 3 game_manager config_uips
		
		// Map time
		globalaccum 0 trigger_if_equal 1 game_manager maptime_ph1
		
		// wm_announce "Phase 1 enabled"
		accum 1 trigger_if_equal 0 game_manager generator_active
		accum 8 trigger_if_equal 0 game_manager alliedcp_active
		
		// doors //
		trigger middle_flag_door open
		trigger middle_transmitter_door close
		
		// signs //
		trigger gen_allies_tst gen_yellow
		trigger sidewall red
		trigger depot_gate_controls red
		trigger documents red
		trigger documents tred
		
		// forward bunker (flag) stuff
		trigger Forwardbunker_flag hide
		
		// spawn stuff
		setautospawn "Generator Spawn" 0

	}
	
	trigger phase2
	{
		// Map time
		globalaccum 0 trigger_if_equal 1 game_manager maptime_ph2
		wait 500
		
		// clean up from phase1
		accum 1 trigger_if_equal 1 game_manager generator_complete
		
		// new objectives
		accum 7 trigger_if_equal 0 game_manager gaxiscp_active
		accum 3 trigger_if_equal 0 game_manager forward_bunker_active
		accum 2 trigger_if_equal 0 game_manager sidewall_active
		accum 4 trigger_if_equal 0 game_manager depotgatecontrols_active
		
		// forward bunker (flag) stuff
		trigger Forwardbunker_flag show
		
		// depo gate stuff
		trigger depogateswitchdoor closed
		trigger depogateswitchdoor2 closed
		
		// doors //
		trigger gen_gate open
		trigger sidewall_gate open
		trigger cheese_door open
		trigger main_upper_door disable_slow

		// signs //
		trigger gen_allies_tst gen_blue
		trigger sidewall yellow
		trigger depot_gate_controls yellow
		// trigger documents red
		// trigger documents tred
		
		// spawn stuff
		// trigger Forwardbunker_flag show
		trigger genspawn hide
 		setautospawn "Forward bunker" 0
		setstate Maingate_cmm invisible
	}
	
	trigger phase3
	{
		// Map Time
		globalaccum 0 trigger_if_equal 1 game_manager maptime_ph3
		wait 500
		
		// wm_announce "Phase 3 enabled"
		// clean up from phase 2
		accum 3 trigger_if_equal 1 game_manager forward_bunker_complete
		accum 2 trigger_if_equal 1 game_manager sidewall_complete
		accum 4 trigger_if_equal 1 game_manager depotgatecontrols_complete
		
		// new objectives
		accum 9 trigger_if_equal 0 game_manager ramp_active
		accum 5 trigger_if_equal 0 game_manager documents_active
			
		// doors //
		trigger middle_flag_door close
		trigger middle_transmitter_door open
		
		// signs //
		trigger documents yellow
	}
	
	trigger capture
	{
		wm_objective_status 6 1 1
		wm_objective_status 6 0 2
		trigger game_manager transmitter_complete
		wm_announce "^4Allies ^2have transmitted the Documents^7!"
		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "axis_hq_objective_lost"
		wm_teamvoiceannounce 1 "allies_hq_objective_secure"
		// *---------------------------------------------------------------------------------*
		wm_setwinner 1
		wait 1500

		wm_endround
	}
	
	trigger generator_active
	{
		accum 1 set	0
		wm_addteamvoiceannounce 0 "fragginhart_axis_generator_defend"
		wm_addteamvoiceannounce 1 "fragginhart_generator_attack"
		wm_teamvoiceannounce 0 "fragginhart_axis_generator_defend"
		wm_teamvoiceannounce 1 "fragginhart_generator_attack"
	}

	trigger sidewall_active
	{
		accum 2 set	0
		wm_addteamvoiceannounce 1 "fragginhart_allies_entrance2_destroy" //side
		wm_addteamvoiceannounce 0 "fragginhart_axis_entrance2_stop" //side
		wm_teamvoiceannounce 1 "fragginhart_allies_entrance2_destroy" //side
		wm_teamvoiceannounce 0 "fragginhart_axis_entrance2_stop" //side
	}

	trigger forward_bunker_active
	{
		accum 3 set	0
		wm_addteamvoiceannounce 0 "fragginhart_axis_bunker_stop"
		wm_addteamvoiceannounce 1 "fragginhart_allies_bunker_capture"
		wm_teamvoiceannounce 0 "fragginhart_axis_bunker_stop"
		wm_teamvoiceannounce 1 "fragginhart_allies_bunker_capture"
	}

	trigger depotgatecontrols_active
	{
		accum 4 set	0
		wm_addteamvoiceannounce 0 "fragginhart_axis_gates_stop"
		wm_addteamvoiceannounce 1 "fragginhart_allies_entrance1_destroy"
		wm_teamvoiceannounce 0 "fragginhart_axis_gates_stop"
		wm_teamvoiceannounce 1 "fragginhart_allies_entrance1_destroy"
	}

	trigger documents_active
	{
		accum 5 set	0
		wm_addteamvoiceannounce 0 "fragginhart_axis_def_docs"
		wm_addteamvoiceannounce 1 "fragginhart_allies_take_docs"
		wm_teamvoiceannounce 0 "fragginhart_axis_def_docs"
		wm_teamvoiceannounce 1 "fragginhart_allies_take_docs"
	}

	trigger transmitter_active
	{
		accum 6 set 0
	}

	trigger axiscp_active
	{
		accum 7 set	0
		wm_addteamvoiceannounce 0 "axis_hq_compost_construct"
	}
	
	trigger alliedcp_active
	{
		accum 8 set	0
		wm_addteamvoiceannounce 0 "fragginhart_axis_cp_stop"
		wm_addteamvoiceannounce 1 "allies_hq_compost_construct"
		wm_teamvoiceannounce 0 "fragginhart_axis_cp_stop"
		wm_teamvoiceannounce 1 "allies_hq_compost_construct"
	}
	
	trigger ramp_active
	{
		accum 9 set	0
		wm_addteamvoiceannounce 0 "fragginhart_axis_ramp_stop"
		wm_addteamvoiceannounce 1 "fragginhart_ramp_construct"
		wm_teamvoiceannounce 0 "fragginhart_axis_ramp_stop"
		wm_teamvoiceannounce 1 "fragginhart_ramp_construct"
	}
	
	trigger generator_complete
	{
		accum 1 set	1
		wm_removeteamvoiceannounce 0 "fragginhart_axis_generator_defend"
		wm_removeteamvoiceannounce 1 "fragginhart_generator_attack"
	}

	trigger sidewall_complete
	{
		accum 2 set	1
		wm_removeteamvoiceannounce 1 "fragginhart_allies_entrance2_destroy" //side
		wm_removeteamvoiceannounce 0 "fragginhart_axis_entrance2_stop" //side
	}

	trigger forward_bunker_complete
	{
		accum 3 set	1
		wm_removeteamvoiceannounce 0 "fragginhart_axis_bunker_stop"
		wm_removeteamvoiceannounce 1 "fragginhart_allies_bunker_capture"
	}

	trigger depotgatecontrols_complete
	{
		accum 4 set	1
		wm_removeteamvoiceannounce 0 "fragginhart_axis_gates_stop"
		wm_removeteamvoiceannounce 1 "fragginhart_allies_entrance1_destroy"
	}

	trigger documents_complete
	{
		accum 5 set	1
		wm_removeteamvoiceannounce 0 "fragginhart_axis_def_docs"
		wm_removeteamvoiceannounce 1 "fragginhart_allies_take_docs"
	}

	trigger transmitter_complete
	{
		accum 6 set 1
	}

	trigger axiscp_complete
	{
		accum 7 set	1
		wm_removeteamvoiceannounce 0 "axis_hq_compost_construct"
	}

	trigger alliedcp_complete
	{
		accum 8 set	1
		wm_removeteamvoiceannounce 0 "fragginhart_axis_cp_stop"
		wm_removeteamvoiceannounce 1 "allies_hq_compost_construct"
	}
	
	trigger ramp_complete
	{
		accum 9 set	1
		wm_removeteamvoiceannounce 0 "fragginhart_axis_ramp_stop"
		wm_removeteamvoiceannounce 1 "fragginhart_ramp_construct"
	}
	
	trigger allies_cp_spawn
	{
		accum 8 abort_if_equal 0
		accum 1 abort_if_equal 0
		trigger allied_cp_open cp_spawn_enable		
	}
	
}

// for blocking the windows in the documents room
docswindow
{
	spawn
	{
		wait 500
	}
	trigger unblock
	{
		setstate docswindow invisible
	}
	trigger block
	{
		setstate docswindow default
	}
}
// for blocking the upper maingate office
uppermaingateoffice_door
{
	spawn
	{
		accum 0 set	0 //default is closed
		accum 1 set	1 //default enable
		wait 500
	}
	trigger open
	{
		accum 1 abort_if_equal 0
		accum 0 abort_if_equal 1
		playsound sound/movers/doors/door5_open.wav volume 200
		gotomarker umgo_door_open_pos 80 wait
		stopsound
		accum 0 set	1
	}
	trigger close
	{
		accum 1 abort_if_equal 0
		accum 0 abort_if_equal 0
		gotomarker umgo_door_closed_pos 80 wait
		stopsound
		accum 0 set	0
	}
	trigger disabled
	{
		accum 1 set	0
	}
	
	trigger enabled
	{
		accum 1 set	1
	}
}
/// script movers for blocking the villa garage route from allies spawn.
villa_garage
{
	spawn
	{
		wait 500
	}
	
	trigger block
	{
		setstate villa_garage default
	}
	
	trigger unblock
	{
		setstate villa_garage invisible
	}
}

/// script movers for blocking the battery room.
battery_room
{
	spawn
	{
		wait 500
		// trigger self unblock
	}
	
	trigger block
	{
		setstate battery_room default
	}
	
	trigger unblock
	{
		setstate battery_room invisible
	}
}

// for blocking the axis middle spawn to forward bunker archway. 
middle_flag_door
{
	spawn
	{
		accum 0 set	0 //default is closed
		wait 500
		// trigger self open
	}
	trigger open
	{
		accum 0 abort_if_equal 1
		alertentity middle_flag_door
		accum 0 set	1
	}
	trigger close
	{
		accum 0 abort_if_equal 0
		alertentity middle_flag_door
		accum 0 set	0
	}
}

// brush for blocking the window looking onto the generator from sidewall room.
block_gen_sidewall_window
{
	spawn
	{
		wait 500
		// trigger self false
	}
	
	trigger false
	{
		setstate block_gen_sidewall_window invisible
	}
	
	trigger true
	{
		setstate block_gen_sidewall_window default
	}
}

// blocks the passage between the sidewall and the flag/bottom of maingate stairs.
block_flag_sidewall
{
	spawn
	{
		wait 500
		// trigger self false
	}
	
	trigger false
	{
		setstate block_flag_sidewall invisible
	}
	
	trigger true
	{
		setstate block_flag_sidewall default
	}
}

middle_transmitter_door
{
	spawn
	{
		accum 0 set	0 //default is closed
		wait 500
		// trigger self open
	}
	trigger open
	{
		accum 0 abort_if_equal 1
		alertentity middle_transmitter_door
		accum 0 set	1
	}
	trigger close
	{
		accum 0 abort_if_equal 0
		alertentity middle_transmitter_door
		accum 0 set	0
	}
}

// ==== Forward Flag ==== //

Forwardbunker_flag
{
	spawn
	{
		accum 0 set 2													// Who owns flag: 0-Axis, 1-Allied, 2-Nobody

		wait 50
		setstate map_inst_axis default
		setstate map_inst_allies invisible
	}

	trigger axis_capture			// Touched by an Axis player 
	{
		accum 0 abort_if_equal 0										// do Axis own flag?
		accum 0 trigger_if_equal 1 Forwardbunker_flag axis_reclaim		// Reclaimed from Allies
		accum 0 set 0													// Axis own the flag

		//setautospawn "Forward bunker" 0						// Disable setautospawn
		// setautospawn "Villa" 1
		setstate map_inst_axis default
		setstate map_inst_allies invisible
	}

	trigger axis_reclaim 
	{
		// setautospawn "Forward bunker" 0								// Disable setautospawn
		// setautospawn "Villa" 1
		trigger allied_cp_open enable_autospawn
		alertentity Forwardbunker_wobj									// Switch command map marker

		wm_objective_status 3 1 0
		wm_objective_status 3 0 0

		wm_announce "^1Axis ^2reclaim the Forward Bunker^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 0 "fragginhart_axis_bunker_stop"
		wm_addteamvoiceannounce 1 "fragginhart_allies_bunker_capture"

		wm_teamvoiceannounce 0 "fragginhart_axis_bunker_reclaimed"
		wm_teamvoiceannounce 1 "fragginhart_allies_bunker_reclaimed"
		// *---------------------------------------------------------------------------------*
	}

	trigger allied_capture												// Touched by an allied player
	{
		accum 0 abort_if_equal 1										// do Allies own flag?
		accum 0 set 1													// Allied own the flag

		alertentity Forwardbunker_wobj									// Switch command map marker
		// setautospawn "Middle Spawn" 0									// Disable setautospawn
		setautospawn "Forward bunker" 1
		trigger allied_cp_open no_autospawn

		setstate map_inst_axis invisible
		setstate map_inst_allies default

		wm_objective_status 3 1 1
		wm_objective_status 3 0 2

		wm_announce "^4Allies ^2captured the Forward Bunker^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "fragginhart_axis_bunker_captured"
		wm_teamvoiceannounce 1 "fragginhart_allies_bunker_captured"

		wm_removeteamvoiceannounce 0 "fragginhart_axis_bunker_stop"
		wm_removeteamvoiceannounce 1 "fragginhart_allies_bunker_capture"     
		// *---------------------------------------------------------------------------------*
	}

	trigger allied_reclaim 
	{
		//setautospawn "Axis Spawn" 0									// Disable setautospawn
		setautospawn "Forward bunker" 1
		trigger allied_cp_open no_autospawn
	}

	trigger disabled													// Disable Flag
	{
		trigger allied_cp_open no_autospawn
		setstate Forwardbunker_flag invisible
		trigger game_manager forward_bunker_complete
		accum 0 abort_if_equal 1 // do Allies own flag?
		trigger Forwardbunker_flag allied_capture
		alertentity Forwardbunker_spawns
	}

	trigger enabled														// Enable Flag
	{
		setstate Forwardbunker_flag default
		trigger allied_cp_open enable_autospawn
		trigger game_manager generator_active
		accum 0 abort_if_equal 0 // do Axis own flag?
		trigger Forwardbunker_flag axis_capture
		alertentity Forwardbunker_spawns
	}
	trigger hide
	{
		setstate Forwardbunker_flag invisible
		setstate Forwardbunker_flag_base invisible
	}
	trigger show
	{
		setstate Forwardbunker_flag default
		setstate Forwardbunker_flag_base default
	}
}

middle_spawn
{
	spawn
	{
		trigger self enable
	}
	
	trigger enable
	{
		setstate middle_spawn default
		setstate middle_spawn_twobj default
		accum 0 set 1
	}
	
	trigger disable
	{
		setstate middle_spawn invisible
		setstate middle_spawn_twobj invisible
		accum 0 set 0
	}
}

middle_spawn_ladder_door
{
	spawn
	{
		wait 500
		trigger self enable
	}
	
	trigger middle_spawn_ladder_door_toggle
	{
		alertentity middle_spawn_ladder_door
	}
	
	trigger disable
	{
		trigger self middle_spawn_ladder_door_toggle
		wait 500
		setstate middle_spawn_ladder_door invisible
	}
	
	trigger enable
	{
		setstate middle_spawn_ladder_door default
	}
	
	trigger lock
	{
		setstate middle_spawn_ladder_door_tm invisible
	}
	
	trigger unlock
	{
		setstate middle_spawn_ladder_door_tm default
	}
}


// ==== Side Wall ==== //
Sidewall
{
	spawn
	{
		wait 300
		constructible_class 3     // 2=Satchel 3=Dyna
		setstate bit_of_sidewall invisible
	}

	death
	{
		wm_objective_status 2 1 1
		wm_objective_status 2 0 2
		trigger game_manager sidewall_complete
		wm_announce "^4Allies ^2have destroyed the Side Wall^7!"
		setstate bit_of_sidewall default
		trigger self blue
		trigger depot_gate_controls yellow
		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 0 "fragginhart_axis_entrance2_destroyed"
		wm_teamvoiceannounce 1 "fragginhart_allies_entrance2_destroyed"zz

		wm_removeteamvoiceannounce 0 "fragginhart_axis_entrance2_stop"
		wm_removeteamvoiceannounce 1 "fragginhart_allies_entrance2_destroy"
		// *---------------------------------------------------------------------------------*
	}
	
	trigger red
	{
		setstate sidewall_yellow invisible
		setstate sidewall_red default
		setstate sidewall_blue invisible
	}
	
	trigger blue
	{
		setstate sidewall_yellow invisible
		setstate sidewall_red invisible
		setstate sidewall_blue default
	}
	
	trigger yellow
	{
		setstate sidewall_yellow default
		setstate sidewall_red invisible
		setstate sidewall_blue invisible
	}
}

sidewall_gate
{
	spawn
	{
		accum 1 set 1 //default close
	}
	trigger open
	{
		accum 1 abort_if_equal 0
		alertentity sidewall_gate
		accum 1 set 0
	}
	trigger close
	{
		accum 1 abort_if_equal 1
		alertentity sidewall_gate
		accum 1 set 1
	}
}

/////// depo gate upper cov door
depo_upper_door //old cov op door
{
    spawn
	{
	}
	
	trigger depo_upper_door_toggle
	{
	    alertentity cov_door_Depot_Gate
		// playsound sound/movers/switches/switch_02.wav volume 100
	}
	
	trigger disable
	{
	    alertentity cov_door_Depot_Gate
		wait 500
		setstate cov_door_Depot_Gate invisible
		// playsound sound/movers/switches/switch_02.wav volume 100
	}
	trigger disable_slow
	{
		wait 10000
	    alertentity cov_door_Depot_Gate
		wait 500
		setstate cov_door_Depot_Gate invisible
		// playsound sound/movers/switches/switch_02.wav volume 100
	}
}


// ==== Depot Gate ==== //

move_Depot_Gate
{
	spawn
	{
		wait 50
		setstate button2_Depot_Gate invisible
		setstate button1_Depot_Gate default

	}

	trigger open_action_Depot_Gate
	{
		trigger Depot_Gate open
		trigger depogateswitch on
		trigger depogateswitch2 on
		wait 11000
		// trigger Depot_Gate close
	}

	trigger close_action_Depot_Gate
	{	
		trigger Depot_Gate close
		trigger depogateswitch off
		trigger depogateswitch2 off
		// wait 11000
	}

	trigger dead_action_Depot_Gate
	{	
		trigger Depot_Gate dead
		// trigger basement_spawn enable
		// trigger middle_spawn disable
		trigger Forwardbunker_flag disabled
		// wait 11000
	}

	trigger alive_action_Depot_Gate
	{
		trigger Depot_Gate alive
	 // trigger Forwardbunker_flag enabled							// Axis can not take flag back
	}
}

Depot_Gate
{
	spawn
	{
	}

	trigger alive
	{
		playsound sound/movers/misc/garage_door_loop_01.wav looping volume 1024

		gotomarker Depot_Gate_closed_pos 50 wait

		stopsound

		wm_objective_status 4 1 0
		wm_objective_status 4 0 0

		// wm_announce "Axis have repaired the Depot Gate Controls!"
		setstate button2_Depot_Gate invisible
		setstate button1_Depot_Gate default
	
		setstate cov_door_Depot_Gate default
	}

	trigger dead
	{
		setstate button2_Depot_Gate invisible
		setstate button1_Depot_Gate invisible
		playsound sound/movers/misc/garage_door_loop_01.wav looping volume 1024

		gotomarker Depot_Gate_dead_pos 50 wait
		// gotomarker Depot_Gate_open_pos 50 wait							// Depot Gate will open 100% on death

		stopsound

		// wm_announce "Allies have damaged the Depot Gate Controls!"
		
		playsound sound/movers/misc/garage_door_end_01.wav volume 1024

		wait 80

	}

	trigger open
	{

		// wm_announce "^7Depot Gate is ^2opening^7!"
		setstate button1_Depot_Gate invisible
		setstate button2_Depot_Gate default
		
		playsound sound/movers/misc/garage_door_loop_01.wav looping volume 1024

		gotomarker Depot_Gate_open_pos 50 wait
		// gotomarker Depot_Gate_dead_pos 50 wait							// Depot Gate will open 50% on button

		stopsound

	}
	
	trigger open_slow
	{
		setstate button2_Depot_Gate invisible
		setstate button1_Depot_Gate invisible

		playsound sound/movers/misc/garage_door_loop_01.wav looping volume 1024

		gotomarker Depot_Gate_dead_pos 5 wait
		faceangles 0 0 2 500
		// gotomarker Depot_Gate_open_pos 5 wait							// Depot Gate will open 100% on death
		trigger fallingbeam dead
		stopsound

		// wm_announce "Allies have damaged the Depot Gate Controls!"
		
		playsound sound/movers/misc/garage_door_end_01.wav volume 1024

		wait 80

	}
	
	trigger close
	{
		// trigger depogateswitch off
		setstate button2_Depot_Gate invisible
		setstate button1_Depot_Gate default
		// wm_announce "^7Depot Gate is ^1closing^7!"

		playsound sound/movers/misc/garage_door_loop_01.wav looping volume 1024

		gotomarker Depot_Gate_closed_pos 50 wait

		stopsound
	}

}

depogateswitchdoor
{
	spawn
	{
		trigger self open
	}

	trigger init
	{
		faceangles 0 -90 0 1000
	}

	trigger closed
	{
		faceangles 0 -90 0 2000
	}

	trigger open
	{
		faceangles 0 70 0 2000
		// playsound sound/world/icecrack.wav volume 256

	}
		
	trigger dead
	{
		faceangles 0 -90 0 1000
	}
}

depogateswitch
{
	spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles 0 0 -80 1000
	}

	trigger off
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		faceangles 0 0 0 250
		faceangles 0 0 -80 250
		// playsound sound/movers/switches/switch_01.wav volume 128
	}

	trigger on
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		faceangles 0 0 0 250
		faceangles 0 0 80 250
		//playsound sound/movers/switches/switch_01.wav volume 128
	}
	
	trigger momentry
	{
		faceangles 80 0 0 250
		faceangles 0 0 0 1000
	}
	
	trigger dead
	{
		faceangles 0 0 90 1000
	}
}

depogateswitchdoor2
{
	spawn
	{
		trigger self open
	}

	trigger init
	{
		faceangles 0 -90 0 1000
	}

	trigger closed
	{
		faceangles 0 -90 0 2000
		setstate depogateswitchdoor2 invisible
		setstate depogateswitch2 invisible
		setstate depogateswitch1 invisible
		trigger depot_gate_controls show
		trigger Depot_Gate close
	}

	trigger open
	{
		faceangles 0 70 0 2000
		// playsound sound/world/icecrack.wav volume 256

	}
		
	trigger dead
	{
		faceangles 0 -90 0 1000
	}
}

depogateswitch2
{
	spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles 0 0 80 1000
	}

	trigger off
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		faceangles 0 0 0 250
		faceangles 0 0 80 250
		// playsound sound/movers/switches/switch_01.wav volume 128
	}

	trigger on
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		faceangles 0 0 0 250
		faceangles 0 0 -80 250
		//playsound sound/movers/switches/switch_01.wav volume 128
	}
	
	trigger momentry
	{
		faceangles 80 0 0 250
		faceangles 0 0 0 1000
	}
	
	trigger dead
	{
		faceangles 0 0 90 1000
	}
}

// ==== Depot Gate Controls ==== //


depot_gate_controls
{
	spawn
	{
		wait 300
		constructible_class 3
		setstate Depot_Gate_deadplate invisible
		setstate SecondCovDoor_axisconstruct_n default
		accum 4 set 0 //red 0 blue 1 yellow 2
		trigger self hide
	}

	death
	{
		accum 4 set 1 //red 0 blue 1 yellow 2
		wm_objective_status 4 1 1
		wm_objective_status 4 0 2
		wm_teamvoiceannounce 1 "fragginhart_allies_gates_destroyed"
		wm_teamvoiceannounce 0 "fragginhart_axis_gates_destroyed"
		wm_announce "^4Allies ^2have damaged the Depot Gate Controls^7!"
		// wm_announce "^2Depot Gate is opening^7!"
		setstate Depot_Gate_deadplate default
		trigger game_manager depotgatecontrols_complete
		trigger Forwardbunker_flag disabled
		trigger self open_all
		trigger self blue
		trigger game_manager phase3

	}
	trigger show
	{
		setstate depot_gate_controls default
		setstate depot_gate_controls_info default
	}
	
	trigger hide
	{
		setstate depot_gate_controls invisible
		setstate depot_gate_controls_info invisible
	}
	
	trigger open_all
	{
		trigger depo_upper_door disable_slow
		setstate SecondCovDoor_axisconstruct_n invisible
		alertentity depo_fb_door
		trigger Depot_Gate open_slow
	}
	
	trigger red
	{
		setstate depogate_red_ctl default
		setstate depogate_blue_ctl invisible
		setstate depogate_yellow_ctl invisible
	}
	
	trigger blue
	{
		setstate depogate_red_ctl invisible
		setstate depogate_blue_ctl default
		setstate depogate_yellow_ctl invisible
	}
	
	trigger yellow
	{
		accum 4 abort_if_not_equal 0
		setstate depogate_red_ctl invisible
		setstate depogate_blue_ctl invisible
		setstate depogate_yellow_ctl default
	}
}

fallingbeam
{
	spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles 0 0 0 1000
	}

	trigger dead
	{
		playsound sound/world/metalbreak.wav volume 256
		faceangles 0 0 -31 400
	}
}

/// scriptmover to block the basement under the documents room
depot_basement_block
{
	spawn
	{
		wait 500
	}
	trigger true
	{
		setstate depot_basement_block default
	}
	
	trigger false
	{
		setstate depot_basement_block invisible
	}
}


/// scrptmover to block the depo reception to stair route
depot_recep
{
	spawn
	{
		wait 500
		// trigger self unblock
	}
	trigger block
	{
		setstate depot_recep default
	}
	
	trigger unblock
	{
		setstate depot_recep invisible
	}
}


/// scriptmover to block depot southeast store room 
depot_se_storeroom
{
	spawn
	{
		wait 500
	}
	trigger block
	{
		setstate depot_se_storeroom default
	}
	
	trigger unblock
	{
		setstate depot_se_storeroom invisible
	}
}

/// Depo yard elevator/lift
Depot_lift
{
	spawn
	{	
		wait 500
	}
	
	trigger up
	{
		accum 0 abort_if_equal 3
		playsound sound/movers/motors/motor_start_01.wav volume 200
		playsound sound/movers/motors/motor_loop_01.wav  looping volume 200
		gotomarker Depot_lift_up_pos 40 wait
		stopsound
		playsound sound/movers/motors/motor_end_01.wav volume 200
		trigger self doors_open
		wait 2500
		accum 0 abort_if_equal 3
		setstate Depot_lift_down_trigger default
		setstate Depot_lift_up_trigger invisible
	}
	
	trigger down
	{
		accum 0 abort_if_equal 3
		trigger self doors_closed
		wait 2500
		playsound sound/movers/motors/motor_start_01.wav volume 200
		playsound sound/movers/motors/motor_loop_01.wav  looping volume 200
		gotomarker Depot_lift_down_pos 40 wait
		stopsound
		playsound sound/movers/motors/motor_end_01.wav volume 200
		accum 0 abort_if_equal 3
		setstate Depot_lift_down_trigger invisible
		setstate Depot_lift_up_trigger default
	}
	
	trigger doors_open
	{
		playsound sound/vehicles/misc/radar_start.wav volume 200
		trigger Depot_lift_door_l open
		trigger Depot_lift_door_r open
		
	}
	
	trigger doors_closed
	{
		playsound sound/vehicles/misc/radar_end.wav volume 200
		trigger Depot_lift_door_l close
		trigger Depot_lift_door_r close
	}
	
	trigger disabled_up
	{
		accum 0 set 3
		gotomarker Depot_lift_up_pos 40 wait
		trigger self doors_open
		setstate Depot_lift_down_trigger invisible
		setstate Depot_lift_up_trigger invisible
		setstate Depot_lift_up_tst invisible
		setstate Depot_lift_down_tst invisible
		setstate depo_lift_switch_a invisible
		setstate depo_lift_switch_b invisible
		setstate depo_lift_switch_c invisible
	}

	trigger disabled_down
	{
		accum 0 set 3
		gotomarker Depot_lift_down_pos 40 wait
		trigger self doors_closed
		setstate Depot_lift_down_trigger invisible
		setstate Depot_lift_up_trigger invisible
		setstate Depot_lift_up_tst invisible
		setstate Depot_lift_down_tst invisible
		setstate depo_lift_switch_a invisible
		setstate depo_lift_switch_b invisible
		setstate depo_lift_switch_c invisible
	}
	
	trigger enabled
	{
		accum 0 set 0
		setstate Depot_lift_down_trigger default
		setstate Depot_lift_up_trigger default
		setstate Depot_lift_up_tst default
		setstate Depot_lift_down_tst default
		setstate depo_lift_switch_a default
		setstate depo_lift_switch_b default
		setstate depo_lift_switch_c default
		wait 3000
		trigger Depot_lift_up_tst push
	}
}

Depot_lift_up_tst
{
	trigger push
	{
		trigger depo_lift_switch_a up
		trigger depo_lift_switch_b up
		trigger depo_lift_switch_c up
		trigger Depot_lift up
		setstate Depot_lift_down_trigger invisible
		setstate Depot_lift_up_trigger invisible
	}
}

Depot_lift_down_tst
{
	trigger push
	{
		trigger depo_lift_switch_a down
		trigger depo_lift_switch_b down
		trigger depo_lift_switch_c down
		trigger Depot_lift down
		setstate Depot_lift_down_trigger invisible
		setstate Depot_lift_up_trigger invisible
	}
}

Depot_lift_door_l
{
	spawn
	{
		accum 0 set	2
	}
	
	trigger open
	{
		//accum 0 abort_if_equal 0
		gotomarker Depot_lift_door_l_open_pos 40 wait
		accum 0 set	0
	}
	
	trigger close
	{
		//accum 0 abort_if_equal 1
		gotomarker Depot_lift_door_l_close_pos 40 wait
		accum 0 set	1
	}
}

Depot_lift_door_r
{
	spawn
	{
		accum 0 set	0
	}
	
	trigger open
	{
		//accum 0 abort_if_equal 0
		gotomarker Depot_lift_door_r_open_pos 40 wait
		accum 0 set	0
	}
	
	trigger close
	{
		//accum 0 abort_if_equal 1
		gotomarker Depot_lift_door_r_close_pos 40 wait
		accum 0 set	1
	}
}

depo_lift_switch_a
{
	spawn
	{
		faceangles 70 0 0 100
	}
	
	trigger up
	{	
		faceangles 0 0 0 100
		faceangles 70 0 0 100
		playsound sound/movers/switches/switch_02.wav volume 100
	}
	
	trigger down
	{
		faceangles 0 0 0 100
		faceangles -70 0 0 100
		playsound sound/movers/switches/switch_02.wav volume 100
	}
}

depo_lift_switch_b
{
	spawn
	{
		faceangles 70 0 0 100
	}
	
	trigger up
	{	faceangles 0 0 0 100
		faceangles 70 0 0 100
		playsound sound/movers/switches/switch_02.wav volume 100
	}
	
	trigger down
	{
		faceangles 0 0 0 100
		faceangles -70 0 0 100
		playsound sound/movers/switches/switch_02.wav volume 100
	}
}

depo_lift_switch_c
{
	spawn
	{
		faceangles 0 0 70 100
	}
	
	trigger up
	{	faceangles 0 0 0 100
		faceangles 0 0 70 100
		playsound sound/movers/switches/switch_02.wav volume 100
	}
	
	trigger down
	{
		faceangles 0 0 0 100
		faceangles 0 0 -70 100
		playsound sound/movers/switches/switch_02.wav volume 100
	}
}

//////// generator stuff
gen_controls
{
	spawn
	{
		wait 200
		constructible_class 2
		constructible_constructxpbonus 1
		constructible_destructxpbonus 1
		trigger self disable
	}
	built final
	{
		setstate gen_controls default
		// setstate gen_controls invisible
		trigger gen_sound start
		trigger alarm_box alarm_one
		trigger gen_allies_tst start_count
		setstate gen_controls_toi invisible
	}
	decayed final
	{
		setstate gen_controls invisible
	}
	death
	{
		trigger gen_allies_tst stop_count
		trigger alarm_box stop
	}
	
	trigger enable
	{
		setstate gen_controls default
		setstate gen_controls_toi default	
	}
	
	trigger disable
	{
		setstate gen_controls invisible
		setstate gen_controls_toi invisible
	}
}


switchlever
{
	spawn
	{
		wait 500
		trigger self init
	}

	trigger init
	{
		faceangles 64 0 0 1000

	}

	trigger off
	{
		playsound sound/movers/switches/switch_01.wav volume 100
		faceangles 64 0 0 500
		trigger gen_sound stop
		trigger alarm_box stop
		wm_teamvoiceannounce 1 "fragginhart_allies_sw_low"
		wm_teamvoiceannounce 1 "fragginhart_allies_sw_inst"
		wm_teamvoiceannounce 0 "fragginhart_axis_sw_low"
	}

	trigger on
	{
		playsound sound/movers/switches/switch_01.wav volume 100
		faceangles -76 0 0 500
		trigger gen_sound start
		trigger gen_sound idle
		trigger alarm_box alarm_one
		wm_teamvoiceannounce 1 "fragginhart_allies_sw_raised"
		wm_teamvoiceannounce 0 "fragginhart_axis_sw_raised"
		wm_teamvoiceannounce 0 "fragginhart_axis_sw_inst"
	}
	
	trigger disable
	{
		trigger gen_axis_tst disable
	}
	
	trigger enable
	{
		trigger gen_axis_tst enable
	}
}

gen_axis_tst
{
	spawn
	{
		// trigger self hide
		trigger self enable
	}
	trigger press
	{
		
		// trigger gen_sound toggle
		trigger self hide
		trigger switchlever off
		trigger gen_allies_tst stop_count

	}
	trigger hide
	{
		setstate gen_button_axis invisible
		setstate gen_button_allies default

	}
	
	trigger disable
	{
		setstate gen_button_axis invisible
		setstate gen_button_allies invisible

	}
	
	trigger enable
	{
		setstate gen_button_axis default
		setstate gen_button_allies default
		trigger self hide
		trigger switchlever off
	}
}

gen_allies_tst
{
	spawn
	{
		accum 0 set 0
		accum 1 set 0
		accum 2 set 0
		trigger self gen_yellow
	}
	
	trigger press
	{
		// trigger gen_sound toggle
		trigger self hide
		trigger switchlever on
		trigger gen_allies_tst start_count
	}
	
	trigger hide
	{
		setstate gen_button_axis default
		setstate gen_button_allies invisible
	}
	
	trigger tick
	{
		globalaccum 1 trigger_if_equal 0 gen_allies_tst tick_00
		globalaccum 1 trigger_if_equal 1 gen_allies_tst tick_10
		globalaccum 1 trigger_if_equal 2 gen_allies_tst tick_20
		globalaccum 1 trigger_if_equal 3 gen_allies_tst tick_30
	}

	trigger tick_00
	{
		trigger gen_allies_tst charge
		wait 1000
		accum 1 abort_if_equal 0
		trigger gen_allies_tst count
		trigger gen_allies_tst tick
	}
	
	trigger tick_10
	{
		trigger gen_allies_tst charge
		wait 1000
		accum 1 abort_if_equal 0
		trigger gen_allies_tst count
		trigger gen_allies_tst tick
	}
	
	trigger tick_20
	{
		trigger gen_allies_tst charge
		wait 2000
		accum 1 abort_if_equal 0
		trigger gen_allies_tst count
		trigger gen_allies_tst tick
	}
	
	trigger tick_30
	{
		trigger gen_allies_tst charge
		wait 3000
		accum 1 abort_if_equal 0
		trigger gen_allies_tst count
		trigger gen_allies_tst tick
	}
	
	trigger count
	{
		accum 2 inc 1
	}
	
	trigger reset_count
	{
		accum 2 set 0
	}
	
	trigger charge
	{
		
		accum 2 trigger_if_equal 0 gen_allies_tst precent_000
		accum 2 abort_if_equal 0
		accum 2 trigger_if_equal 1 gen_allies_tst precent_010
		accum 2 abort_if_equal 1
		accum 2 trigger_if_equal 2 gen_allies_tst precent_020
		accum 2 abort_if_equal 2
		accum 2 trigger_if_equal 3 gen_allies_tst precent_030
		accum 2 abort_if_equal 3
		accum 2 trigger_if_equal 4 gen_allies_tst precent_040
		accum 2 abort_if_equal 4
		accum 2 trigger_if_equal 5 gen_allies_tst precent_050
		accum 2 abort_if_equal 5
		accum 2 trigger_if_equal 6 gen_allies_tst precent_060
		accum 2 abort_if_equal 6
		accum 2 trigger_if_equal 7 gen_allies_tst precent_070
		accum 2 abort_if_equal 7
		accum 2 trigger_if_equal 8 gen_allies_tst precent_080
		accum 2 abort_if_equal 8
		accum 2 trigger_if_equal 9 gen_allies_tst precent_090
		accum 2 abort_if_equal 9
		accum 2 trigger_if_equal 10 gen_allies_tst precent_100	
	}
	
	trigger precent_000
	{
		wm_announce "^7Power^b:^1=^1=^1=^1=^1=^1=^1=^1=^1=^1=^b:"
	}
	
	trigger precent_010
	{
		wm_announce "^7Power^b:^2=^1=^1=^1=^1=^1=^1=^1=^1=^1=^b:"
	}
	
	trigger precent_020
	{
		wm_announce "^7Power^b:^2=^2=^1=^1=^1=^1=^1=^1=^1=^1=^b:"
	}

	trigger precent_030
	{
		wm_announce "^7Power^b:^2=^2=^2=^1=^1=^1=^1=^1=^1=^1=^b:"
	}
	
	trigger precent_040
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^1=^1=^1=^1=^1=^1=^b:"
	}

	trigger precent_050
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^2=^1=^1=^1=^1=^1=^b:"
	}

	trigger precent_060
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^2=^2=^1=^1=^1=^1=^b:"
	}
	
	trigger precent_070
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^2=^2=^2=^1=^1=^1=^b:"
	}

	trigger precent_080
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^2=^2=^2=^2=^1=^1=^b:"
	}
	
	trigger precent_090
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^2=^2=^2=^2=^2=^1=^b:"
	}

	trigger precent_100
	{
		wm_announce "^7Power^b:^2=^2=^2=^2=^2=^2=^2=^2=^2=^2=^b:"
		trigger gen_axis_tst disable
		trigger gen_sound start
		trigger gen_sound on
		trigger gen_allies_tst capture
	}
	
	trigger start_count
	{
		accum 1 set 1
		globalaccum 1 trigger_if_equal 1 powerneedle move
		globalaccum 1 trigger_if_equal 2 powerneedle move
		globalaccum 1 trigger_if_equal 3 powerneedle move
		trigger gen_allies_tst tick
	}
	
	trigger stop_count
	{
		accum 1 set 0
		wm_announce "^1Axis ^2have stopped the Generator!^7..."
		globalaccum 1 trigger_if_equal 0 gen_allies_tst reset_count
		globalaccum 1 abort_if_equal 0
		trigger powerneedle stopneedle
	}
	
	trigger capture
	{
		trigger game_manager generator_complete
		trigger game_manager allies_cp_spawn
		globalaccum 1 trigger_if_equal 0 powerneedle move
		globalaccum 1 trigger_if_equal 1 powerneedle idle
		globalaccum 1 trigger_if_equal 2 powerneedle idle
		globalaccum 1 trigger_if_equal 3 powerneedle idle
		trigger alarm_box stop
		trigger radio_speaker enable
		trigger the_dropzone show
 		// trigger move_Main_Gate alive_action_Main_Gate
		trigger Main_Gate alive_open_slow
		trigger game_manager phase2
 		wm_objective_status 1 1 1
 		wm_objective_status 1 0 2
 		// *----------------------------------- vo ------------------------------------------*
 		wm_teamvoiceannounce 0 "axis_hq_objective_lost"
 		wm_teamvoiceannounce 1 "allies_hq_objective_secure"
		// *---------------------------------------------------------------------------------*
		// wm_announce "^2Main Gate Controls are ^1enabled^7."
	}
	
 	trigger gen_red
 	{
 	    setstate generator_blue_sign invisible
 		setstate generator_red_sign default
 		setstate generator_yellow_sign invisible
 	}
 	
 	trigger gen_blue
 	{
 	    setstate generator_blue_sign default
 		setstate generator_red_sign invisible
 		setstate generator_yellow_sign invisible
 	}
 	
 	trigger gen_yellow
 	{
 	    setstate generator_blue_sign invisible
 		setstate generator_red_sign invisible
 		setstate generator_yellow_sign default
 	}

}

powerneedle
{
	spawn
	{
		accum 0 set 0
		accum 1 set 0
		accum 9 set 0
		faceangles 0 0 89 1
	}
	trigger move
	{
		globalaccum 1 trigger_if_equal 0 powerneedle move_00
		globalaccum 1 trigger_if_equal 1 powerneedle move_10
		globalaccum 1 trigger_if_equal 2 powerneedle move_20
		globalaccum 1 trigger_if_equal 3 powerneedle move_30
	}
	
	trigger move_00
	{
		setrotation 0 0 -17
		wait 10000
		trigger powerneedle idle
	}
	
	trigger move_10
	{
		setrotation 0 0 -17
	}
	
	trigger move_20
	{
		setrotation 0 0 -8
	}
	
	trigger move_30
	{
		setrotation 0 0 -5
	}
	
	trigger stopneedle
	{
		accum 1 set 0
		stopsound
		stoprotation
	}
	
	trigger reset
	{
		faceangles 0 0 89 1
		accum 0 set 0
	}
	
	trigger idle_sound
	{
		accum 1 abort_if_equal 3
		playsound sound/world/fan_slow.wav looping volume 200
		accum 1 set 3
	}
	trigger idle
	{
		faceangles 0 0 -80 1
		wait 500
		trigger powerneedle idle_run
	}
	
	trigger idle_run
	{
		trigger powerneedle idle_sound
		setrotation 0 0 2
		wait 250
		stoprotation
		wait 250
		setrotation 0 0 -2
		wait 250
		trigger powerneedle idle_run
	}
}

gen_sound
{
	spawn 
	{
	}
	
	trigger toggle
	{
		alertentity gen_sound
	}
	
	trigger start
	{
		stopsound
		playsound sound/movers/motors/motor_end_01.wav volume 20
	}
	
	trigger stop
	{
		stopsound
		playsound sound/movers/motors/motor_end_01.wav volume 20
	}
	
	trigger idle
	{
		stopsound
		playsound sound/movers/motors/motor_loop_01.wav looping volume 30
	}
	
	trigger on
	{
		stopsound
		playsound sound/movers/motors/motor_loop_03.wav looping volume 100
	}
}

gen_gate
{
	spawn
	{
		accum 0 set 0
		wait 500
	}
	
	trigger open
	{
		accum 0 abort_if_equal 1
		accum 0 abort_if_equal 3
		alertentity gen_gate
		accum 0 set 1
	}
	
	trigger close
	{
		accum 0 abort_if_equal 0
		accum 0 abort_if_equal 3
		alertentity gen_gate
		accum 0 set 0
	}
	
	trigger disabled
	{
		wait 500
		accum 0 set 3
	}
	
	trigger enabled
	{
		wait 500
		accum 0 set 0
	}
}



// ==== Main Gate ==== //

maingateswitch
{
		spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles 90 0 0 1000

	}

	trigger off
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		trigger maingateswitch2 off
		faceangles 0 0 0 250
		faceangles 80 0 0 250
		// playsound sound/movers/switches/switch_01.wav volume 128
	}

	trigger on
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		trigger maingateswitch2 on
		faceangles 0 0 0 250
		faceangles -80 0 0 250
		//playsound sound/movers/switches/switch_01.wav volume 128
	}
	
	trigger momentry
	{
		trigger maingateswitch2 momentry
		faceangles -90 0 0 250
		faceangles 0 0 0 1000
	}
	trigger dead
	{
		faceangles 90 0 0 1000
		trigger maingateswitch2 dead
	}
}

maingateswitchdoor
{
	spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles 0 -90 0 1000
	}

	trigger closed
	{
		faceangles 0 -90 0 2000
	}

	trigger open
	{
		faceangles 0 70 0 2000
		// playsound sound/world/icecrack.wav volume 256

	}
		
	trigger dead
	{
		faceangles 0 -90 0 1000
	}
}

maingateswitch2
{
	spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles -80 0 0 1000
	}

	trigger off
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		faceangles 0 0 0 250
		faceangles -80 0 0 250
		// playsound sound/movers/switches/switch_01.wav volume 128
	}

	trigger on
	{
		playsound sound/movers/switches/switch_01.wav volume 128
		faceangles 0 0 0 250
		faceangles 80 0 0 250
		//playsound sound/movers/switches/switch_01.wav volume 128
	}
	
	trigger momentry
	{
		faceangles 80 0 0 250
		faceangles 0 0 0 1000
	}
	
	trigger dead
	{
		faceangles -90 0 0 1000
	}
}
maingateswitch2door
{
	spawn
	{
		trigger self init
	}

	trigger init
	{
		faceangles 0 -90 0 1000
	}

	trigger closed
	{
		faceangles 0 -90 0 2000
	}

	trigger open
	{
		faceangles 0 70 0 2000
		// playsound sound/world/icecrack.wav volume 256

	}
	
	trigger dead
	{
		faceangles 0 -90 0 1000
	}
}

genspawn
{
	trigger show
	{
		setstate genspawn default
		setstate genspawn_twoj default
	}
	trigger hide
	{
		setstate genspawn invisible
		setstate genspawn_twoj invisible
	}
}


main_upper_door //old cov op door
{
	spawn
	{
		accum 0 set 0
	}
	
	trigger main_upper_door_toggle
	{
	    alertentity cov_door_Main_Gate
	}
	
	trigger disable
	{
		alertentity cov_door_Main_Gate
		wait 500
		setstate cov_door_Main_Gate invisible
		trigger uppermaingateoffice_door open
	}
	trigger disable_slow
	{
		wait 10000
		alertentity cov_door_Main_Gate
		wait 500
		setstate cov_door_Main_Gate invisible
		trigger uppermaingateoffice_door open
	}
}

move_Main_Gate
{
	spawn
	{
		wait 50

		setstate button2_Main_Gate invisible
		setstate button1_Main_Gate default
		trigger move_Main_Gate dead_action_Main_Gate
	}

	trigger open_action_Main_Gate
	{
		trigger Main_Gate open
		// wait 11000
	}

	trigger close_action_Main_Gate
	{	
		trigger Main_Gate close
		// wait 11000
	}

	trigger dead_action_Main_Gate
	{	
		trigger Main_Gate dead
		// wait 11000
	}
	
	trigger alive_action_Main_Gate
	{
		trigger Main_Gate alive
	}
}

Main_Gate
{
	spawn
	{
	}

	trigger alive
	{
		//playsound sound/movers/misc/garage_door_loop_01.wav looping volume 1024

		// gotomarker Main_Gate_closed_pos 50 wait
		// gotomarker Main_Gate_open_pos 50 wait

		//stopsound
		trigger maingateswitchdoor open
		trigger maingateswitch2door open
		setstate button2_Main_Gate invisible
		setstate button1_Main_Gate default
	}

	trigger dead
	{
		playsound sound/movers/misc/big_gate1.wav looping volume 1024

		// gotomarker Main_Gate_open_pos 50 wait
		gotomarker Main_Gate_closed_pos 50 wait

		stopsound

		// wm_announce "Main Gate Controls disabled!"

		wait 80
		
		trigger maingateswitch dead
		setstate button_dead_Main_Gate default
		// setstate cov_door_Main_Gate invisible
		setstate button2_Main_Gate invisible
		setstate button1_Main_Gate invisible
		setstate main_door_tunnels default
	}
	
	trigger open
	{
		trigger maingateswitch on
		wait 500
		setstate button1_Main_Gate invisible
		setstate button2_Main_Gate default

		// wm_announce "^7Main Gate is ^2opening^7!"
		playsound sound/movers/misc/big_gate2.wav looping volume 1024

		gotomarker Main_Gate_open_pos 50 wait

		stopsound	
	}
	
	trigger alive_open_slow
	{
		setstate button1_Main_Gate invisible

		// wm_announce "^7Main Gate is ^2opening^7!"
		playsound sound/movers/misc/big_gate2.wav looping volume 1024 //move

		gotomarker Main_Gate_open_pos 5 wait
		stopsound
		trigger maingateswitchdoor open
		trigger maingateswitch2door open
		trigger maingateswitch on
		setstate button1_Main_Gate invisible
		setstate button2_Main_Gate default
	}
	
	trigger close
	{
		trigger maingateswitch off
		wait 500
		setstate button2_Main_Gate invisible
		setstate button1_Main_Gate default

		// wm_announce "^7Main Gate is ^1closing^7!"

		playsound sound/movers/misc/big_gate2.wav looping volume 1024 //move

		gotomarker Main_Gate_closed_pos 50 wait
		stopsound
	}

}

// ==== Ramp ==== //
ramp_alliedconstruct_n
{
	spawn
	{
		wait 50
		trigger self setup
		constructible_class	2											// 2=Satchel 3=Dyna
	}

	trigger setup
	{
		setstate ramp_alliedconstruct_n_materials default				// Crate Models
		setstate ramp_alliedconstruct_n_clip default					// Clip brushes
		setstate ramp_alliedconstruct_n_flag default
		setstate bit_of_wall default
	}

	built final
	{
		setstate ramp_alliedconstruct_n_materials invisible				// Crate Models
		setstate ramp_alliedconstruct_n_clip invisible					// Clip brushes
		setstate ramp_alliedconstruct_n_flag invisible
		setstate bit_of_wall invisible

		wm_announce "^4Allies ^2have built the Ramp^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_removeteamvoiceannounce 0 "fragginhart_axis_ramp_stop"
		wm_removeteamvoiceannounce 1 "fragginhart_ramp_construct"

		wm_teamvoiceannounce 0 "fragginhart_axis_ramp_constructed"
		wm_teamvoiceannounce 1 "fragginhart_ramp_constructed"
		// *---------------------------------------------------------------------------------*

		// trigger ramp_door disable
		// trigger axis_doc_door disable
		trigger game_manager ramp_complete
	}

	decayed final
	{
		trigger self setup
	}

	death
	{
		trigger self setup
		// trigger ramp_door enable
		// trigger axis_doc_door enable

		wm_announce "^1Axis ^2have destroyed the Ramp^7!"
		trigger game_manager ramp_active

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 0 "fragginhart_axis_ramp_stop"
		wm_addteamvoiceannounce 1 "fragginhart_ramp_construct"

		wm_teamvoiceannounce 0 "fragginhart_axis_ramp_destroyed"
		wm_teamvoiceannounce 1 "fragginhart_allies_ramp_destroyed"
		// *---------------------------------------------------------------------------------*
	}
}

// ==== Documents ==== //
documents
{
	spawn
	{
		wait 20
		// bugged
		// trigger the_dropzone hide
		trigger self red
		trigger self tred
		accum 5 set 0 //red 0 blue 1 yellow 2
	}
	trigger stolen
	{
		setstate documents_CMM invisible
		trigger self blue
		trigger self tyellow
		wm_objective_status 5 1 1
		wm_objective_status 5 0 2
		trigger radio_speaker play_allies
	}
	trigger dropped
	{
		wm_announce_icon 5 "^4Allies ^2have dropped the Documents^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_teamvoiceannounce 1 "allies_hq_objective_lost"
		wm_teamvoiceannounce 0 "fragginhart_axis_docs_dropped"
		// *---------------------------------------------------------------------------------*
	}
	trigger returned
	{
		setstate documents_CMM default
		wm_objective_status 5 1 0
		wm_objective_status 5 0 0
		trigger radio_speaker play_axis
		trigger self tred
		trigger self red
		accum 5 abort_if_not_equal 2
		trigger self yellow
	} 
	trigger captured
	{
		trigger game_manager documents_complete
		trigger self blue
		trigger self tblue
	}
	
	trigger red
	{
		setstate docs_red_sign default
		setstate docs_blue_sign invisible
		setstate docs_yellow_sign invisible
	}
	
	trigger blue
	{
		setstate docs_red_sign invisible
		setstate docs_blue_sign default
		setstate docs_yellow_sign invisible
	}
	
	trigger yellow
	{
		setstate docs_red_sign invisible
		setstate docs_blue_sign invisible
		setstate docs_yellow_sign default
		accum 5 set 2 //red 0 blue 1 yellow 2
	}
	
	trigger tred
	{
		setstate transmitter_red default
		setstate transmitter_blue invisible
		setstate transmitter_yellow invisible
	}
	
	trigger tblue
	{
		setstate transmitter_red invisible
		setstate transmitter_blue default
		setstate transmitter_yellow invisible
	}
	
	trigger tyellow
	{
		setstate transmitter_red invisible
		setstate transmitter_blue invisible
		setstate transmitter_yellow default
	}
}

the_dropzone
{
	spawn
	{
		wait 500
		trigger self hide
	}
	death
	{
		trigger game_manager capture
	}

	trigger show
	{
		setstate the_dropzone Default
		setstate transmitter_off_msg invisible
		setstate transmitter_off_msg_trigger invisible
		wait 10
	}

	trigger hide
	{
		setstate the_dropzone invisible
		setstate transmitter_off_msg default
		setstate transmitter_off_msg_trigger default
	}
}
trans_covdoor
{
	spawn
	{
		wait 500
		accum 0 set 0
	}
	
	trigger true
	{
		setstate trans_covdoor default
		setstate trans_olddoor invisible
	}
	
	trigger false
	{
		setstate trans_covdoor invisible
		setstate trans_olddoor default
	}
	
}

radio_speaker
{
	spawn
	{
		wait 500
		accum 0 set 1
		accum 1 set 0
		stopsound
		// setstate transmitter_off_msg default
		// setstate transmitter_off_msg_trigger default
	}
	
	trigger mute
	{
		stopsound
		accum 1 set 0
	}
	
	trigger enable
	{
		// setstate transmitter_off_msg invisible
		// setstate transmitter_off_msg_trigger invisible
		accum 1 set 1
		accum 0 trigger_if_equal 1 radio_speaker play_axis
		accum 0 trigger_if_equal 2 radio_speaker play_allies
	}
	
	trigger play
	{
		accum 1 abort_if_equal 0
		accum 0 trigger_if_equal 1 radio_speaker play_axis
		accum 0 trigger_if_equal 2 radio_speaker play_allies
	}
	
	trigger play_axis
	{
		accum 0 set 1
		stopsound
		playsound sound/remasters/german-radio1.wav volume 150
		wait 10000
		stopsound
		playsound sound/world/radio_static_01.wav volume 300
		wait 4000
		stopsound
		playsound sound/world/radio_static_02.wav volume 300
		wait 5000
		trigger self play
	}
	
	trigger play_allies
	{
		accum 0 set 2
		stopsound
		playsound sound/remasters/bscdb.wav volume 500
		wait 5000
		stopsound
		playsound sound/world/radio_static_01.wav volume 300
		wait 4000
		stopsound
		playsound sound/world/radio_static_02.wav volume 300
		wait 5000
		trigger self play
	}
}

axis_doc_door
{
	spawn
	{
		accum 0 set 0
		accum 1 set 0
		wait 500
		trigger self disable
	}
	
	trigger axis_doc_door_toggle
	{
		alertentity axis_doc_doors
	}
	
	trigger axis_doc_door_open
	{
	    alertentity axis_doc_doors
	}
	
	trigger axis_doc_door_close
	{
	    alertentity axis_doc_doors
	}
	
	trigger disable
	{
		trigger self axis_doc_door_toggle
		wait 500
		setstate axis_doc_doors invisible
	}
	
	trigger enable
	{
		setstate axis_doc_doors default
	}
}

basement_spawn
{
	spawn
	{
		trigger self disable
	}
	
	trigger disable
	{
		setstate basement_spawn invisible
		setstate basement_spawn_twobj invisible
		accum 0 set 0
	}
	
	trigger enable
	{
		setstate basement_spawn default
		setstate basement_spawn_twobj default
		accum 0 set 1
	}
}


// ==== Allied Command Post ==== //

allied_cp_open
{
	spawn
	{
		wait 100
		accum 0 set 0													// 0=CP Closed, 1=CP Damaged
		constructible_class 2											// Dyno+Satchel
		trigger self disable_cp_features								// Default charge bar times
		trigger self cp_spawn_disable
		setstate allied_cp_damaged invisible							// Hide damaged CP clipbrush+model
		setstate allied_cp_damaged_model invisible
	}
	
	trigger cp_spawn_enable
	{
		setstate allies_cp_spawn default
		setstate cpspawn_wobj default
		wm_announce "^4Allied ^2Command Post spawn Enabled^7!"
		accum 8 abort_if_equal 1
		setautospawn "Allies CP Spawn" 1
	}
	
	trigger cp_spawn_disable
	{
		// alertentity cpspawn_wobj
		setstate allies_cp_spawn invisible
		setstate cpspawn_wobj invisible
		wm_announce "^4Allied ^2Command Post spawn Disabled^7!"
	}
	
	trigger no_autospawn
	{
		accum 8 set 1
	}
	
	trigger enable_autospawn
	{
		accum 8 set 0
	}
	
	trigger disable_cp_features
	{
		setstate allied_cp_open_model invisible							// Hide Open CP model

		setchargetimefactor 1 soldier 1
		setchargetimefactor 1 lieutenant 1
		setchargetimefactor 1 medic 1
		setchargetimefactor 1 engineer 1
		setchargetimefactor 1 covertops 1

		sethqstatus 1 0													// Landmines team warning message
		disablespeaker speaker_allied_cp								// Disable morse code sound
		// wm_allied_respawntime 20										// Disable change in Allied respantime
	}

	trigger enable_cp_features
	{
		setstate allied_cp_open_model default							// Show open CP model

		setchargetimefactor 1 soldier 0.75
		setchargetimefactor 1 lieutenant 0.75
		setchargetimefactor 1 medic 0.75
		setchargetimefactor 1 engineer 0.75
		setchargetimefactor 1 covertops 0.75

		sethqstatus 1 1													// Landmines team warning message
		enablespeaker speaker_allied_cp									// Enable morse code sound
		// wm_allied_respawntime 15										// Disable change in Allied respantime
	}

	buildstart final
	{
		setstate allied_cp_closed invisible								// Hide closed CP clipbrush+model
		setstate allied_cp_closed_model invisible
		setstate allied_cp_damaged invisible							// Hide damaged CP clipbrush+model
		setstate allied_cp_damaged_model invisible
	}

	built final
	{
		accum 0 set 1													// Only show the damaged CP model
		trigger self enable_cp_features									// Increase charge bar times
		trigger game_manager alliedcp_complete

		wm_objective_status 8 1 1
		wm_objective_status 8 0 2
		
		trigger game_manager alliedcp_complete
		trigger game_manager allies_cp_spawn
		wm_announce "^4Allied ^2Command Post constructed. Charge speed increased^7!"

		// *----------------------------------- vo -----------------------------------------
		wm_teamvoiceannounce 0 "axis_hq_compost_constructed_allies"
		wm_teamvoiceannounce 1 "allies_hq_compost_constructed"

		wm_removeteamvoiceannounce 1 "allies_hq_compost_construct"
		wm_removeteamvoiceannounce 0 "fragginhart_axis_cp_stop"
		// *---------------------------------------------------------------------------------*
	}

	decayed final
	{
		setstate allied_cp_open_model invisible							// Hide Open CP model

		trigger self show_damaged_cabinet								// Show previous cabinet state
		trigger self show_closed_cabinet
	}

	death
	{
		trigger self show_damaged_cabinet								// Show damaged CP model
		trigger self disable_cp_features								// Default charge bar times
		trigger game_manager alliedcp_active
		trigger self cp_spawn_disable
		
		wm_objective_status 8 1 0
		wm_objective_status 8 0 0
		

		wm_announce "^1Axis ^2have destroyed the ^4Allied ^2Command Post^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 1 "allies_hq_compost_construct"
		wm_addteamvoiceannounce 0 "fragginhart_axis_cp_stop"

		wm_teamvoiceannounce 1 "allies_hq_compost_damaged"

		wm_removeteamvoiceannounce 0 "axis_hq_compost_constructed_allies"
		// *---------------------------------------------------------------------------------*
	}

	trigger show_closed_cabinet
	{
		accum 0 abort_if_equal 1										// Closed cabinet active yet?
		setstate allied_cp_closed default
		setstate allied_cp_closed_model default
	}

	trigger show_damaged_cabinet
	{
		accum 0 abort_if_equal 0										// Damaged cabinet active yet?
		setstate allied_cp_damaged default
		setstate allied_cp_damaged_model default
	}
}

// ==== Axis Command Post ==== //

axis_cp_open
{
	spawn
	{
		wait 100
		accum 0 set 0													// 0=CP Closed, 1=CP Damaged
		constructible_class 2											// Dyno+Satchel

		trigger self disable_cp_features								// Default charge bar times

		setstate axis_cp_damaged invisible								// Hide damaged CP clipbrush+model
		setstate axis_cp_damaged_model invisible
	}

	trigger disable_cp_features
	{
		setstate axis_cp_open_model invisible							// Hide Open CP model

		setchargetimefactor 0 soldier 1
		setchargetimefactor 0 lieutenant 1
		setchargetimefactor 0 medic 1
		setchargetimefactor 0 engineer 1
		setchargetimefactor 0 covertops 1

		sethqstatus 0 0													// Landmines team warning message
		disablespeaker speaker_axis_cp									// Disable morse code sound
		// wm_axis_respawntime 30										// Disable change in Axis respawntime
	}

	trigger enable_cp_features
	{
		setstate axis_cp_open_model default								// Show open CP model

		setchargetimefactor 0 soldier 0.75
		setchargetimefactor 0 lieutenant 0.75
		setchargetimefactor 0 medic 0.75
		setchargetimefactor 0 engineer 0.75
		setchargetimefactor 0 covertops 0.75

		sethqstatus 0 1													// Landmines team warning message
		enablespeaker speaker_axis_cp									// Enable morse code sound
		// wm_axis_respawntime 25										// Disable change in Allies respawntime
	}

	buildstart final
	{
		setstate axis_cp_closed invisible								// Hide closed CP clipbrush+model
		setstate axis_cp_closed_model invisible
		setstate axis_cp_damaged invisible								// Hide damaged CP clipbrush+model
		setstate axis_cp_damaged_model invisible
	}

	built final
	{
		accum 0 set 1													// Only show the damaged CP model
		trigger self enable_cp_features									// Increase charge bar times
		trigger game_manager axiscp_complete

		wm_objective_status 7 1 2
		wm_objective_status 7 0 1

		wm_announce "^1Axis ^2Command Post constructed. Charge speed increased^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 1 "allies_hq_compost_constructed_axis"

		wm_teamvoiceannounce 0 "axis_hq_compost_constructed"
		wm_teamvoiceannounce 1 "allies_hq_compost_constructed_axis"

		wm_removeteamvoiceannounce 0 "axis_hq_compost_construct"
		// *---------------------------------------------------------------------------------*
	}

	decayed final
	{
		setstate axis_cp_open_model invisible							// Hide Open CP model

		trigger self show_damaged_cabinet								// Show previous cabinet state
		trigger self show_closed_cabinet
	}

	death
	{
		trigger self show_damaged_cabinet	 							// Show damaged CP model
		trigger self disable_cp_features								// Default charge bar times
		trigger game_manager axiscp_active
		wm_objective_status 7 1 0
		wm_objective_status 7 0 0

		wm_announce "^4Allies ^2have destroyed the ^1Axis ^2Command Post^7!"

		// *----------------------------------- vo ------------------------------------------*
		wm_addteamvoiceannounce 0 "axis_hq_compost_construct"

		wm_teamvoiceannounce 0 "axis_hq_compost_damaged"

		wm_removeteamvoiceannounce 1 "allies_hq_compost_constructed_axis"
		// *---------------------------------------------------------------------------------*
	}

	trigger show_closed_cabinet
	{
		accum 0 abort_if_equal 1										// Closed cabinet active yet?
		setstate axis_cp_closed default
		setstate axis_cp_closed_model default
	}

	trigger show_damaged_cabinet
	{
		accum 0 abort_if_equal 0										// Damaged cabinet active yet?
		setstate axis_cp_damaged default
		setstate axis_cp_damaged_model default
	}
}

// ==== MG42 Nest ==== //

alliedconstruct1
{
	spawn
	{
		wait 50
		trigger self setup
		constructible_class 2											// 2=Satchel 3=Dyna
	}

	trigger setup
	{
		setstate alliedconstruct1_materials default                     // Crate Models
		setstate alliedconstruct1_clip default							// Clip brushes
		setstate alliedconstruct1_flag default

		setstate alliedconstruct1_extra invisible						// Sandbags
		setstate alliedconstruct1_mg42 invisible						// MG42 entity
		repairmg42 alliedconstruct1_mg42
	}

	built final
	{
		setstate alliedconstruct1_materials invisible					// Crate Models
		setstate alliedconstruct1_clip invisible						// Clip brushes
		setstate alliedconstruct1_flag invisible

		setstate alliedconstruct1_extra default							// Sandbags
		setstate alliedconstruct1_mg42 default							// MG42 entity

		wm_announce "^4Allies ^2have built the MG42 nest^7!"
	}

	decayed final
	{
		trigger self setup
	}

	death
	{
		trigger self setup

		wm_announce "^1Axis ^2have destroyed the MG42 Nest^7!"
	}
}

// === alarm system === //
alarm_box
{
	spawn
	{
		trigger self stop
	}
	
	trigger stop
	{
		stopsound
	}
	
	trigger alarm_one
	{
		playsound sound/world/alarm_01.wav looping volume 500
	}
	
	trigger alarm_two
	{
		playsound sound/world/alarm_02.wav looping volume 500
	}
}

// === The door at transmitter road === //
cheese_door
{
	spawn
	{
		accum 0 set 0
		wait 500
		// trigger self open
	}
	
	trigger open
	{
		accum 0 abort_if_equal 1
		accum 0 abort_if_equal 3
		alertentity cheese_door
		accum 0 set 1
	}
	
	trigger close
	{
		accum 0 abort_if_equal 0
		accum 0 abort_if_equal 3
		alertentity cheese_door
		accum 0 set 0
	}
	
	trigger disable_open
	{
		trigger cheese_door open
		wait 500
		accum 0 set 3
	}
	
	trigger disable_closed
	{
		trigger cheese_door close
		wait 500
		accum 0 set 3
	}
	
	trigger enabled
	{
		wait 500
		accum 0 set 0
	}
}

// little radio at axis middle spawn.
axis_spawn_radio
{
	spawn
	{
		wait 2000
		accum 0 set 0
	}
	
	trigger toggle
	{
		accum 0 trigger_if_equal 0 axis_spawn_radio play_0
		accum 0 trigger_if_equal 1 axis_spawn_radio play_1
		accum 0 trigger_if_equal 2 axis_spawn_radio play_2
		accum 0 trigger_if_equal 3 axis_spawn_radio play_3
		accum 0 trigger_if_equal 4 axis_spawn_radio play_4
		accum 0 trigger_if_equal 5 axis_spawn_radio play_5
		accum 0 trigger_if_equal 6 axis_spawn_radio stop
	}
	
	trigger play_0
	{
		accum 0 abort_if_not_equal 0
		playsound sound/menu/select.wav volume 10
		playsound sound/music/menu_server.wav looping volume 100
		wait 500
		accum 0 set 1
	}
	
	trigger play_1
	{
		accum 0 abort_if_not_equal 1
		playsound sound/menu/select.wav volume 10
		playsound sound/world/radio_static_01.wav looping volume 100
		wait 500
		accum 0 set 2
	}
	
	trigger play_2
	{
		accum 0 abort_if_not_equal 2
		playsound sound/menu/select.wav volume 10
		playsound sound/vo/radar/news_radar.wav looping volume 100
		wait 500
		accum 0 set 3
	}
	
	trigger play_3
	{
		accum 0 abort_if_not_equal 3
		playsound sound/menu/select.wav volume 10
		playsound sound/world/radio_static_02.wav looping volume 100
		wait 500
		accum 0 set 4
	}
	
	trigger play_4
	{
		accum 0 abort_if_not_equal 4
		playsound sound/menu/select.wav volume 10
		playsound sound/vo/battery/news_battery.wav looping volume 100
		wait 500
		accum 0 set 5
	}
	
	trigger play_5
	{
		accum 0 abort_if_not_equal 5
		playsound sound/menu/select.wav volume 10
		playsound sound/world/radio_static_01.wav looping volume 70
		wait 500
		accum 0 set 6
	}
	
	trigger stop
	{
		accum 0 abort_if_not_equal 6
		stopsound
		playsound sound/menu/select.wav volume 10
		wait 500
		accum 0 set 0
	}
}

axis_spawn_radio_tst
{
	trigger push
	{
		trigger axis_spawn_radio toggle
	}
}

// mortarspot stuff
mortarspot_in
{
	spawn
	{
		wait 500
		trigger self false
	}
	
	trigger false
	{
		setstate mortarspot_in invisible
	}
	
	trigger true
	{
		setstate mortarspot_in default
	}
}

mortarspot_tst
{
	spawn
	{
		wait 500
	}
	
	trigger fire
	{
		accum 0 abort_if_not_equal 1
		trigger mortarspot_tst toggle_n1
	}
	
	trigger toggle_n1
	{
		trigger mortarspot_in true
		// wm_announce "^1< ^bRema's hidden mortar nest is occupied^7! ^1>"
		wait 1
		// wm_announce "^1< ^bFire shall rain from the sky^7! ^1>"
		trigger mortarspot_in false
	}
	
	trigger false
	{
		accum 0 set 0
		setstate mortarspot_tst invisible
	}
	
	trigger true
	{
		accum 0 set 1
		setstate mortarspot_tst default
	}
}

